PREFIX  rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX  owl:  <http://www.w3.org/2002/07/owl#>
PREFIX  cco:  <http://www.ontologyrepository.com/CommonCoreOntologies/>
PREFIX  cap: <http://www.ohio.edu/ontologies/manufacturing-capability#>

CONSTRUCT{
cap:demarcates		rdf:type	owl:ObjectProperty.
cco:references		rdf:type	owl:ObjectProperty.
cco:is_measured_by	rdf:type	owl:ObjectProperty.
cco:inheres_in			rdf:type	owl:ObjectProperty.
cco:expects			rdf:type	owl:ObjectProperty.
cco:uses_equation_type	rdf:type	owl:ObjectProperty.
cco:has_decimal_value	rdf:type	owl:DatatypeProperty.
cco:has_string_value	rdf:type	owl:DatatypeProperty.	
?capa	rdf:type ?capaType;
			cap:demarcates	?func;
			cco:references	?ref.
?ref		rdf:type	?refType.			
?capa	cco:is_measured_by	?maxICE;
			cco:is_measured_by	?minICE.

?maxICE	cco:inheres_in			?maxIBE;
			cco:is_measured_by	?maxOrd;
			rdf:type	cco:Equation;
			cco:expects	?arg.
?maxOrd	rdf:type	cco:MaximumOrdinalMeasurementInformation.
?maxIBE	rdf:type	cco:InformationBearingEntity;
			cco:has_string_value ?eq;
			cco:uses_equation_type	?eqType.
?arg		rdf:type	?argType;
			cco:is_tokenized_by	?token.
	
?minICE	cco:inheres_in			?minIBE;
			cco:is_measured_by	?minOrd;
			rdf:type	cco:MeasurementInformationContentEntity.
?minOrd	rdf:type	cco:MinimumOrdinalMeasurementInformation.
?minIBE	rdf:type	cco:InformationBearingEntity;
			cco:has_decimal_value	?min.	
}
WHERE{
?p	rdf:type	?pType.
?p cco:realizes ?func.
?capa	rdf:type ?capaType;
			cap:demarcates	?func;
			cco:references	?ref.
?ref		rdf:type	?refType.			
?capa	cco:is_measured_by	?maxICE;
			cco:is_measured_by	?minICE.

?maxICE	cco:inheres_in			?maxIBE;
			cco:is_measured_by	?maxOrd;
			rdf:type	cco:Equation;
			cco:expects	?arg.
?maxOrd	rdf:type	cco:MaximumOrdinalMeasurementInformation.
?maxIBE	rdf:type	cco:InformationBearingEntity;
			cco:has_string_value ?eq;
			cco:uses_equation_type	?eqType.
?arg		rdf:type	?argType;
			cco:is_tokenized_by	?token.
	
?minICE	cco:inheres_in			?minIBE;
			cco:is_measured_by	?minOrd;
			rdf:type	cco:MeasurementInformationContentEntity.
?minOrd	rdf:type	cco:MinimumOrdinalMeasurementInformation.
?minIBE	rdf:type	cco:InformationBearingEntity;
			cco:has_decimal_value	?min.	
FILTER (?pType NOT IN (owl:NamedIndividual))
FILTER (?capaType NOT IN (owl:NamedIndividual))
FILTER (?refType NOT IN (owl:NamedIndividual))
FILTER (?argType NOT IN (owl:NamedIndividual))
FILTER (?eqType NOT IN (owl:NamedIndividual))
}	


